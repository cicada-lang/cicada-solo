@module lang1

{
  ctx = Map.empty
  x = Exp.v("x")
  A = Ty.nat

  { Check(ctx, Exp.fn("x", x), Ty.arrow(A, A))
    ------------------------------------------- Check.fn
    Check(Map.extend(ctx, "x", A), x, A)
    ------------------------------------------- Check.v
    Map.Lookup(Map.extend(ctx, "x", A), "x", A)
    ------------------------------------------- Map.Lookup.here }
}

{
  ctx = Map.empty
  x = Exp.v("x")
  A = Ty.nat
  B = Ty.arrow(Ty.nat, Ty.nat)

  { Check(ctx, Exp.fn("x", Exp.fn("y", x)), Ty.arrow(A, Ty.arrow(B, A)))
    --------------------------------------------------------------------- Check.fn
    Check(Map.extend(ctx, "x", A), Exp.fn("y", x), Ty.arrow(B, A))
    ------------------------------------------------------------------ Check.fn
    Check(Map.extend(Map.extend(ctx, "x", A), "y", B), x, A)
    ---------------------------------------------------------------- Check.v
    Map.Lookup(Map.extend(Map.extend(ctx, "x", A), "y", B), "x", A)
    ----------------------------------------------------------------- Map.Lookup.there
    Map.Lookup(Map.extend(ctx, "x", A), "x", A)
    ------------------------------------------- Map.Lookup.here }
}

{
  ctx = Map.extend(Map.empty, "y", Ty.nat)
  x = Exp.v("x")
  y = Exp.v("y")
  id = Exp.fn("x", x)
  A = Ty.nat

  { Check(ctx, Exp.ap(id, y), A)
    ---------------------------------------- Check.ap
    { Check(ctx, id, Ty.arrow(A, A))
      ---------------------------------------- Check.fn
      Check(Map.extend(ctx, "x", A), x, A)
      ----------------------------------------------- Check.v
      Map.Lookup(Map.extend(ctx, "x", A), "x", A)
      ---------------------------------------------- Map.Lookup.here }
    { Check(ctx, y, A)
      ---------------------------------------- Check.v
      Map.Lookup(ctx, "y", A)
      ---------------------------------------- Map.Lookup.here } }
}
