---
title: Initial and Terminal Objects
---

# Dependencies

```cicada
import { Category } from "./02-category.md"
```

# Initial

```cicada
class Initial {
  cat: Category

  object: cat.Object

  morphism(x: cat.Object): cat.Morphism(object, x)

  morphism_unique(
    implicit x: cat.Object,
    f: cat.Morphism(object, x),
  ): Equal(cat.Morphism(object, x), f, morphism(x))
}
```

# Terminal

```cicada
class Terminal {
  cat: Category

  object: cat.Object
  morphism(x: cat.Object): cat.Morphism(x, object)
  morphism_unique(
    implicit x: cat.Object,
    f: cat.Morphism(x, object),
  ): Equal(cat.Morphism(x, object), f, morphism(x))
}
```

If a terminal object exists, it is unique up to unique isomorphism.

```cicada
import { Isomorphism } from "./02-category.md"
import { equal_swap, equal_compose } from "../equality/01-equal-utilities.md"

function terminal_object_isomorphism(
  cat: Category,
  x: Terminal(cat),
  y: Terminal(cat),
): Isomorphism(cat, x.object, y.object) {
  return {
    cat,
    dom: x.object,
    cod: y.object,
    morphism: y.morphism(x.object),
    inverse: x.morphism(y.object),

    inverse_left: equal_compose(
      x.morphism_unique(
        cat.compose(
          y.morphism(x.object),
          x.morphism(y.object),
        ),
      ),
      equal_swap(x.morphism_unique(cat.id(x.object)))
    ),

    inverse_right: equal_compose(
      y.morphism_unique(
        cat.compose(
          x.morphism(y.object),
          y.morphism(x.object),
        ),
      ),
      equal_swap(y.morphism_unique(cat.id(y.object)))
    ),
  }
}
```
