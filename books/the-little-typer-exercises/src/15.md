---
title: 15. Imagine That ...
---

# ex falso quodlibet -- from false, anything

The eliminator `absurd_ind` corresponds to a principle of thought.

If a false statement were true, then we might as well say anything at all.
a.k.a. "Principle of Explosion" or "ex falso quodlibet", which means "from false, anything."

- https://en.wikipedia.org/wiki/Principle_of_explosion

# `nat_equal_consequence`

|         | zero    | add1(x)          |
|---------|---------|------------------|
| zero    | Trivial | Absurd           |
| add1(y) | Absurd  | Equal(Nat, x, y) |

``` cicada
nat_equal_consequence_t(x: Nat, y: Nat): Type {
  nat_rec(
    x,
    nat_rec(
      y,
      Trivial,
      (_, _) => Absurd),
    (prev_x, _) =>
      nat_rec(
        y,
        Absurd,
        (prev_y, _) => Equal(Nat, prev_x, prev_y)))
}

nat_equal_consequence_same(x: Nat): nat_equal_consequence_t(x, x) {
  nat_ind(
    x,
    (target) => nat_equal_consequence_t(target, target),
    sole,
    (_prev, _almost) => refl)
}
```

The proof of `nat_equal_consequence_t` for Nats that are the same
can be used to prove `nat_equal_consequence_t` for any two equal Nats.

# Imagine That ...

Using types, it is possible to assume things that may or
may not be true, and then see what can be concluded
from these assumptions.

# Two (or more) kinds of equalities

There are more things equal than are the same.
Sameness is a simple kind of (more general) equality.

- `refl` -- definitional and computational equality
- `Equal` -- propositional equality -- requires proof

The fact that any two expressions either are or are not the same means that
we are freed from the obligation to provide a proof
because reflness can be determined by following the Laws and Commandments.

Equality requires proof, and therefore is more expressive.
Recognizing a proof requires only the Laws and Commandments,
but constructing a proof may require creativity, ingenuity, and hard work.

# `use_nat_equal`

use `nat_equal_consequence_same` to prove `nat_equal_consequence_t` for any two equal Nats.

``` cicada
replace_t: Type = (
  X: Type,
  from: X, to: X,
  target: Equal(X, from, to),
  motive: (X) -> Type,
  base: motive(from),
) -> motive(to)

use_nat_equal(
  x: Nat, y: Nat,
  nat_equal: Equal(Nat, x, y),
): nat_equal_consequence_t(x, y) {
  motive(n: Nat): Type {
    nat_equal_consequence_t(x, n)
  }

  base: motive(x) = nat_equal_consequence_same(x)

  replace(nat_equal, motive, base)
}
```

# from false, anything

We can use `use_nat_equal` to use absurd `nat_equal` to prove anything.

The first step is to prove "zero does not equal any Nat that has addl at the top."
This statement is sometimes called no "confusion" or "disjointness".

``` cicada
// zero_add1_equal_absurd(
//   n: Nat,
//   zero_add1_equal: Equal(Nat, zero, add1(n)),
// ): Absurd =
//   use_nat_equal(zero, add1(n), zero_add1_equal)
```

We can also use currying.

``` cicada
zero_add1_equal_absurd(n: Nat): (Equal(Nat, zero, add1(n))) -> Absurd {
  use_nat_equal(zero, add1(n))
}
```

Now prove `donut_absurdity`.

``` cicada
donut_absurdity(
  zero_six_equal: Equal(Nat, 0, 6)
): Equal(String, "powdered", "glazed") {
  absurd_ind(
    zero_add1_equal_absurd(5, zero_six_equal),
    Equal(String, "powdered", "glazed"))
}
```

``` cicada
sub1_equal(
  x: Nat, y: Nat,
): (Equal(Nat, add1(x), add1(y))) -> Equal(Nat, x, y) {
  use_nat_equal(add1(x), add1(y))
}

import { add } from "./03.md"

sub_n_equal(
  x: Nat, y: Nat,
  n: Nat,
): (Equal(Nat, add(n, x), add(n, y))) -> Equal(Nat, x, y) {
  motive(target: Nat): Type {
    (Equal(Nat, add(target, x), add(target, y))) -> Equal(Nat, x, y)
  }

  base: (Equal(Nat, x, y)) -> Equal(Nat, x, y) = (equal) => equal

  step(
    prev: Nat,
    almost: (Equal(Nat, add(prev, x), add(prev, y))) -> Equal(Nat, x, y),
  ): (Equal(Nat, add(add1(prev), x), add(add1(prev), y))) -> Equal(Nat, x, y) {
    (equal) => almost(sub1_equal(add(prev, x), add(prev, y), equal))
  }

  nat_ind(n, motive, base, step)
}
```

Now prove `黑铁换黄金`.

``` cicada
黑铁换黄金(
  equal: Equal(Nat, 37, 64)
): Equal(String, "黑铁", "黄金") {
  absurd_ind(
    zero_add1_equal_absurd(26, sub_n_equal(0, 27, 37, equal)),
    Equal(String, "黑铁", "黄金"))
}
```

# `front` -- like `vector_head`

Absurd is useful for more than just statements involving "not."

Just as `list_ind` can do anything that `list_rec` can do,
`vectro_ind` can do anything that `vector_head` can do.
Sometimes, however, `Absurd` is a necessary part of such definitions.

The base case of `vector_ind` should be ruled out because of false equality.

- We can abstract the false equality out as argument of the motive's return type,
  thus we do not simply return `E`,
  but return `(j: Nat, equal: Equal(Nat, length, add1(j))) -> E`,
- Then in base case, we can use these argument to construct absurd,
  and prove the result by absurd.

``` cicada
front(E: Type, n: Nat, vector: Vector(E, add1(n))): E {
  motive(length: Nat, target: Vector(E, length)): Type {
    (j: Nat, equal: Equal(Nat, length, add1(j))) -> E
  }

  aux: motive(add1(n), vector) =
    base: motive(zero, vecnil) =
      (j, equal) => absurd_ind(zero_add1_equal_absurd(j, equal), E)

    step(
      prev: Nat,
      head: E,
      tail: Vector(E, prev),
      almost: motive(prev, tail)
    ): motive(add1(prev), vec(head, tail)) {
      (j, equal) => head
    }

    vector_ind(add1(n), vector, motive, base, step)

  aux(n, refl)
}

front(Nat, 0, vec! [1])
front(Nat, 1, vec! [1, 2])
front(Nat, 2, vec! [1, 2, 3])
front(Nat, 3, vec! [1, 2, 3, 4])
```

# `rest`

``` cicada
rest(E: Type, n: Nat, vector: Vector(E, add1(n))): Vector(E, n) {
  motive(length: Nat, target: Vector(E, length)): Type {
    (j: Nat, equal: Equal(Nat, length, add1(j))) -> Vector(E, j)
  }

  aux: motive(add1(n), vector) =

    base: motive(zero, vecnil) =
      (j, equal) => absurd_ind(zero_add1_equal_absurd(j, equal), Vector(E, j))

    step(
      prev: Nat,
      head: E,
      tail: Vector(E, prev),
      almost: (j: Nat, equal: Equal(Nat, prev, add1(j))) -> Vector(E, j)
    ): (j: Nat, equal: Equal(Nat, add1(prev), add1(j))) -> Vector(E, j) {
      (j, equal) => replace(sub1_equal(prev, j, equal), (x) => Vector(E, x) , tail)
    }

    vector_ind(add1(n), vector, motive, base, step)

  aux(n, refl)
}

rest(Nat, 0, vec! [1])
rest(Nat, 1, vec! [1, 2])
rest(Nat, 2, vec! [1, 2, 3])
rest(Nat, 3, vec! [1, 2, 3, 4])
```

# `drop_last`

``` cicada
// TODO
```

# tertium non datur -- there is no third choice

Principle of the Excluded Middle,
a.k.a tertium non datur, means "there is no third choice."

``` cicada
pem_t(X: Type): Type {
  Either(X, (X) -> Absurd)
}
```

But in our system there is no evidence for `pem`.

Evidence for `pem` would be a function that determines
the truth or falsity of every statement that can be written as a type.

This would mean that there are no unsolved problems.

Life would be boring if we had no problems left to solve.

But although `pem` isn't true, but it can't possibly be false!

``` cicada
pem_absurd_absurd(
  X: Type,
  pem_absurd: (Either(X, (X) -> Absurd)) -> Absurd,
): Absurd {
  pem_absurd(inr((x) => pem_absurd(inl(x))))
}
```

This means evidence that a statement is not false
is less interesting (or say, much weaker) than evidence that it is true.

# decidable statements

There are, however, some statements that are either true or false.
These statements are called decidable because there is a function
that decides whether they are true or false.

``` cicada
Decide(X: Type): Type {
  Either(X, (X) -> Absurd)
}
```

This is the same as `pem_t`,
because another way to phrase `pem` is
"All statements are decidable."
