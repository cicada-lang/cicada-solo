---
title: 9. Double Your Money, Get Twice as Much
date: 2021-05-26
---

# Exercises

``` cicada
import { cong } from "./08.md"

add(x: Nat, y: Nat): Nat {
  nat_rec(x, y, (_prev, almost) => add1(almost))
}

// NOTE increase
incr(x: Nat): Nat {
  nat_rec(x, 1, (_prev, almost) => add(1, almost))
}

the(Equal(Nat, incr(3), add1(3)), refl)

incr_add1_equal_motive(n: Nat): Type {
  Equal(Nat, incr(n), add1(n))
}

incr_add1_equal_base: Equal(Nat, incr(zero), add1(zero)) = refl

incr_add1_equal_step_t: Type = (
  prev: Nat,
  almost: Equal(Nat, incr(prev), add1(prev))
) -> Equal(Nat, add1(incr(prev)), add1(add1(prev)))

// NOTE Let's prove `incr_add1_equal_step` use `replace` directly.
incr_add1_equal_step(
  prev: Nat,
  almost: Equal(Nat, incr(prev), add1(prev))
): Equal(Nat, add1(incr(prev)), add1(add1(prev))) {
  motive(n: Nat): Type {
    Equal(Nat, add1(incr(prev)), add1(n))
  }

  base: Equal(Nat, add1(incr(prev)), add1(incr(prev))) = refl

  replace(almost, motive, base)
}

incr_add1_equal(n: Nat): Equal(Nat, incr(n), add1(n)) {
  nat_ind(
    n,
    incr_add1_equal_motive,
    incr_add1_equal_base,
    incr_add1_equal_step)
}

incr_add1_equal(0)
incr_add1_equal(1)
incr_add1_equal(2)
incr_add1_equal(3)

double(n: Nat): Nat {
  nat_rec(n, 0, (_prev, almost) => add1(add1(almost)))
}

double(1)
double(2)
double(3)
double(4)

twice(n: Nat): Nat {
  add(n, n)
}

twice(1)
twice(2)
twice(3)
twice(4)

add1_add_commute_on_2nd_arg(n: Nat, j: Nat): Equal(Nat, add1(add(n, j)), add(n, add1(j))) {
  motive(j: Nat, target: Nat): Type {
    Equal(Nat, add1(add(target, j)), add(target, add1(j)))
  }

  base: Equal(Nat, add1(add(0, j)), add(0, add1(j))) = refl

  step(
    prev: Nat,
    almost: Equal(Nat, add1(add(prev, j)), add(prev, add1(j)))
  ): Equal(Nat, add1(add1(add(prev, j))), add1(add(prev, add1(j)))) {
    cong(almost, add(1))
  }

  nat_ind(n, motive(j), base, step)
}

twice_double_equal_step(
  prev: Nat,
  almost: Equal(Nat, add(prev, prev), double(prev))
): Equal(Nat, add1(add(prev, add1(prev))), add1(add1(double(prev)))) {
  target: Equal(Nat, add1(add(prev, prev)), add(prev, add1(prev))) =
    add1_add_commute_on_2nd_arg(prev, prev)

  motive(target: Nat): Type {
    Equal(Nat, add1(target), add1(add1(double(prev))))
  }

  base: Equal(Nat, add1(add1(add(prev, prev))), add1(add1(double(prev)))) =
    cong(almost, add(2))

  replace(target, motive, base)
}

twice_double_equal(n: Nat): Equal(Nat, twice(n), double(n)) {
  motive(target: Nat): Type {
    Equal(Nat, twice(target), double(target))
  }

  base: Equal(Nat, 0, 0) = refl

  nat_ind(n, motive, base, twice_double_equal_step)
}

twice_double_equal(17)

twice_double_equal_on_17: Equal(Nat, twice(17), double(17)) = twice_double_equal(17)
twice_double_equal_on_17_again: Equal(Nat, twice(17), double(17)) = refl

vector_double(
  E: Type,
  length: Nat,
): (Vector(E, length)) -> Vector(E, double(length)) {
  motive(target: Nat): Type {
     (Vector(E, target)) -> Vector(E, double(target))
  }

  base: (Vector(E, 0)) -> Vector(E, double(0)) = (vector) => vecnil

  step(prev: Nat, doubler: motive(prev)): motive(add1(prev)) {
    (vector) => {
      head = vector_head(vector)
      vec(head, vec(head, doubler(vector_tail(vector))))
    }
  }

  nat_ind(length, motive, base, step)
}

vector_double(String, 3, vec! ["a", "b", "c"])

symm(T: Type, from:T, to: T, target: Equal(T, from, to)): Equal(T, to, from) {
  replace(target, (x) => Equal(T, x, from) , refl)
}

vector_twice(
  E: Type,
  length: Nat,
  vector: Vector(E, length)
): Vector(E, twice(length)) {
  target: Equal(Nat, double(length), twice(length)) =
    symm(Nat, twice(length), double(length), twice_double_equal(length))

  motive(length: Nat): Type {
    Vector(E, length)
  }

  base = vector_double(E, length, vector)

  replace(target, motive, base)
}

vector_twice(String, 3, vec! ["a", "b", "c"])
```

# Note about `replace`

But if `replace` replaces the `from` with
the `to`, why should we abstract over the `to`,
rather than the `from`?

Because first we know `motive(to)`,
thus use abstract `to` to get `motive`,
then we try to find exp of type `motive(from)`,
to apply `replace` to get `motive(to)`.
