---
title: 12. Even Numbers Can Be Odd
---

# `Even`

``` cicada
import { double } from "./09.md"

Even(n: Nat): Type {
  (half: Nat) * Equal(Nat, n, double(half))
}

ten_is_even: Even(10) = cons(5, refl)
zero_is_even: Even(0) = cons(0, refl)
```

Although two functions always return the same answer,
sometimes one of them is easier to use
because it more quickly becomes a value.

In particular, `add` and thus `twice`
leave an `add1` on the second argument,
while `double` puts both `add1`s at the top immediately.

**Carefully Choose Definitions**

Carefully-chosen definitions can greatly simplify later proofs.

# `even_add_2_is_even`

``` cicada
import { add } from "./03.md"
import { cong, cong_explicit } from "./08.md"

even_add_2_is_even(n: Nat, even: Even(n)): Even(add(2, n)) {
  half = car(even)
  almost = cdr(even)
  // evidence = cong_explicit(Nat, n, double(half), almost, Nat, add(2))
  evidence = cong(almost, add(2))
  cons(add1(half), evidence)
}

two_is_even: Even(2) = even_add_2_is_even(0, zero_is_even)
```

# `Odd`

``` cicada
Odd(n: Nat): Type {
  // `haf` is the half of the even number, that is one smaller than `n`.
  (haf: Nat) * Equal(Nat, n, add1(double(haf)))
}

eleven_is_odd: Odd(11) = cons(5, refl)
one_is_odd: Odd(1) = cons(0, refl)
```

# `even_add1_is_odd`

``` cicada
even_add1_is_odd(n: Nat, even: Even(n)): Odd(add1(n)) {
  half = car(even)
  almost = cdr(even)
  // evidence = cong_explicit(Nat, n, double(half), almost, Nat, add(1))
  evidence = cong(almost, add(1))
  cons(half, evidence)
}
```

# `odd_add1_is_even`

``` cicada
odd_add1_is_even(n: Nat, odd: Odd(n)): Even(add1(n)) {
  haf = car(odd)
  almost = cdr(odd)
  cons(add1(haf), cong_explicit(Nat, n, add1(double(haf)), almost, Nat, add(1)))
}
```

# Behold! Ackermann!

``` cicada
repeat(f: (Nat) -> Nat, n: Nat): Nat {
  nat_rec(n, f(1), (_prev, almost) => f(almost))
}

ackermann(n: Nat): (Nat) -> Nat {
  nat_rec(n, add(1), (_prev, almost) => repeat(almost))
}

ackermann(0, 0)
ackermann(1, 0)
ackermann(2, 0)
ackermann(3, 0)

ackermann(0, 1)
ackermann(1, 1)
ackermann(2, 1)
ackermann(3, 1)

ackermann(0, 2)
ackermann(1, 2)
ackermann(2, 2)
ackermann(3, 2)

ackermann(0, 3)
ackermann(1, 3)
ackermann(2, 3)
ackermann(3, 3)
```
