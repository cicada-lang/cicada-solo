---
title: 16. If It's All the Same to You
---

# `zero_equal_decide`

``` cicada
import { Decide, use_nat_equal } from "./15.md"

zero_equal_decide(n: Nat): Decide(Equal(Nat, zero, n)) {
  motive(target: Nat): Type {
    Decide(Equal(Nat, zero, target))
  }

  base: Decide(Equal(Nat, zero, zero)) = inl(refl)

  step(
    prev: Nat,
    _almost: Decide(Equal(Nat, zero, prev)),
  ): Decide(Equal(Nat, zero, add1(prev))) {
    inr((equal) => use_nat_equal(zero, add1(prev), equal))
  }

  nat_ind(n, motive, base, step)
}

zero_equal_decide(0)
zero_equal_decide(1)
zero_equal_decide(2)
zero_equal_decide(3)
```

# `nat_equal_decide`

``` cicada
import { add } from "./03.md"
import { cong, cong_explicit } from "./08.md"
import { sub1_equal } from "./15.md"

nat_equal_decide_motive(target: Nat): Type {
  (j: Nat) -> Decide(Equal(Nat, target, j))
}

nat_equal_decide_base: nat_equal_decide_motive(0) = (j) => zero_equal_decide(j)


add1_equal_decide(
  x: Nat, y: Nat,
  eq_or_not: Decide(Equal(Nat, x, y)),
): Decide(Equal(Nat, add1(x), add1(y))) {
  either_ind(
    eq_or_not,
    (_) => Decide(Equal(Nat, add1(x), add1(y))),
    (equal) => {
      // h = cong_explicit(Nat, x, y, equal, Nat, add(1))
      h = cong(equal, add(1))
      inl(h)
    },
    (equal_absurd) => {
      inr((equal) => equal_absurd(sub1_equal(x, y, equal)) )
    })
}

nat_equal_decide_step(
  prev: Nat,
  almost: (j: Nat) -> Decide(Equal(Nat, prev, j)),
  j: Nat,
): Decide(Equal(Nat, add1(prev), j)) {
  motive(target: Nat): Type {
    Decide(Equal(Nat, add1(prev), target))
  }

  base: Decide(Equal(Nat, add1(prev), zero)) =
    inr((equal) => use_nat_equal(add1(prev), zero, equal) )

  step(
    prev_j: Nat,
    // NOTE This inner inductive hypothesis will not be used, use the outer one.
    _almost: Decide(Equal(Nat, add1(prev), prev_j)),
  ): Decide(Equal(Nat, add1(prev), add1(prev_j))) {
    add1_equal_decide(prev, prev_j, almost(prev_j))
  }

  nat_ind(j, motive, base, step)
}

nat_equal_decide(n: Nat, j: Nat): Decide(Equal(Nat, n, j)) {
  nat_ind(
    n,
    nat_equal_decide_motive,
    nat_equal_decide_base,
    nat_equal_decide_step
  )(j)
}

nat_equal_decide(0, 0)
nat_equal_decide(1, 0)
nat_equal_decide(0, 1)
nat_equal_decide(6, 4)
```
