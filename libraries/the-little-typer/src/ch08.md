---
title: 8. Pick a Number, Any Number
---

# Two ways to add1 -- `incr` and `add1`

``` cicada
add(x: Nat, y: Nat): Nat {
  nat_rec(x, y, (_prev, almost) => add1(almost))
}

// NOTE increase
incr(x: Nat): Nat {
  nat_rec(x, 1, (_prev, almost) => add(1, almost))
}

the(Equal(Nat, incr(3), add(1)(3)), refl)
```

`add(1)` is equal to `add1` by reflection.

``` cicada
add_1_add1_equal(n: Nat): Equal(Nat, add(1, n), add1(n)) {
  refl
}
```

But `add(1)` is not equal to `incr` by reflection, we need to prove it.

``` cicada counterexample
the(Equal((Nat) -> Nat, add(1), incr), refl)
```

# `replace` and `cong`

We will use `replace` to define `cong`

``` cicada
replace_t: Type = (
  implicit { X: Type, from: X, to: X },
  target: Equal(X, from, to),
  motive: (X) -> Type,
  base: motive(from),
) -> motive(to)

cong(
  implicit { X: Type, from: X, to: X, },
  target: Equal(X, from, to),
  implicit { Y: Type },
  f: (X) -> Y,
): Equal(Y, f(from), f(to)) {
  replace(
    target,
    (to) => Equal(Y, f(from), f(to)),
    refl)
}
```

# `incr_add1_equal`

``` cicada
incr_add1_equal_motive(n: Nat): Type {
  Equal(Nat, incr(n), add1(n))
}

incr_add1_equal_base: Equal(Nat, incr(zero), add1(zero)) = refl

incr_add1_equal_step_t: Type = (
  prev: Nat,
  almost: Equal(Nat, incr(prev), add1(prev))
) -> Equal(Nat, add1(incr(prev)), add1(add1(prev)))

incr_add1_equal_step(
  prev: Nat,
  almost: Equal(Nat, incr(prev), add1(prev))
): Equal(Nat, add1(incr(prev)), add1(add1(prev))) {
  cong(almost, add(1))
}

incr_add1_equal(n: Nat): Equal(Nat, incr(n), add1(n)) {
  nat_ind(
    n,
    incr_add1_equal_motive,
    incr_add1_equal_base,
    incr_add1_equal_step)
}
```

Some example proofs.

``` cicada
incr_add1_equal(0)
incr_add1_equal(1)
incr_add1_equal(2)
incr_add1_equal(3)
```

# `cong_opts` -- with option arguments

``` cicada
cong_opts(
  opts: class {
    X: Type,
    Y: Type,
    from: X,
    to: X,
  },
  target: Equal(opts.X, opts.from, opts.to),
  f: (opts.X) -> opts.Y,
): Equal(opts.Y, f(opts.from), f(opts.to)) {
  replace(
    target,
    (to) => Equal(opts.Y, f(opts.from), f(to)),
    refl)
}

incr_add1_equal_step_by_cong_opts(
  prev: Nat,
  almost: Equal(Nat, incr(prev), add1(prev))
): Equal(Nat, add1(incr(prev)), add1(add1(prev))) {
  cong_opts({
    X: Nat,
    Y: Nat,
    from: incr(prev),
    to: add1(prev),
  }, almost, add(1))
}

incr_add1_equal_by_cong_opts(n: Nat): Equal(Nat, incr(n), add1(n)) {
  nat_ind(
    n,
    incr_add1_equal_motive,
    incr_add1_equal_base,
    incr_add1_equal_step_by_cong_opts)
}
```

# `cong_explicit` -- without implicit arguments

``` cicada
cong_explicit(
  X: Type, from: X, to: X,
  target: Equal(X, from, to),
  Y: Type,
  f: (X) -> Y,
): Equal(Y, f(from), f(to)) {
  replace(
    target,
    (to) => Equal(Y, f(from), f(to)),
    refl)
}

incr_add1_equal_step_by_cong_explicit(
  prev: Nat,
  almost: Equal(Nat, incr(prev), add1(prev))
): Equal(Nat, add1(incr(prev)), add1(add1(prev))) {
  cong_explicit(Nat, incr(prev), add1(prev), almost, Nat, add(1))
}

incr_add1_equal_by_cong_explicit(n: Nat): Equal(Nat, incr(n), add1(n)) {
  nat_ind(
    n,
    incr_add1_equal_motive,
    incr_add1_equal_base,
    incr_add1_equal_step_by_cong_explicit)
}
```
